apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

id="geneticrypt"
group = "com.geneticrypt"
version = "SNAPSHOT"

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-9'
}

repositories {
    mavenCentral()
}

idea.module.ext {
    gradleCacheVariable = 'GRADLE_CACHE'
    downloadJavadoc = true
}

idea.project {
    jdkName = '1.6'
    wildcards = ['!?*.java', '!?*.groovy', '!?*.scala']
}

idea.project.ipr.withXml { provider ->
    def node = provider.asNode()
    // Use git
    def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
    vcsConfig.mapping[0].'@vcs' = 'Git'

    // Set gradle home
    def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
    gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir.absolutePath])
}

sourceCompatibility = 1.6
compileJava.enabled = false
sourceSets.main.groovy.srcDirs = ["src/main/java", "src/main/groovy"]

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.7.10'

    compile group: 'com.google.guava', name: 'guava', version: '10.0.1'
    compile 'commons-collections:commons-collections:3.2.1'
    compile 'commons-lang:commons-lang:2.3'

    testCompile 'junit:junit:4.10'
    testCompile 'org.hamcrest:hamcrest-core:1.3.RC2'
    testCompile 'org.hamcrest:hamcrest-library:1.3.RC2'
}


task "create-dirs" << {
    sourceSets*.java.srcDirs*.each { it.mkdirs() }
    sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}